
MAIN MODEL Main_VRPTW

  SECTION VRPTW_Model


    DECLARATION SECTION 

      PARAMETER:
         identifier   :  CustomersNumber
         range        :  integer
         definition   :  25 ;

      SET:
         identifier   :  Customers
         subset of    :  Integers
         indices      :  i, j, k
         definition   :  {0..CustomersNumber} ;

      PARAMETER:
         identifier   :  Demand
         index domain :  (i)
         comment      :  "Lower Bound of the Time Window" ;

      PARAMETER:
         identifier   :  LBTW
         index domain :  (i) ;

      PARAMETER:
         identifier   :  UBTW
         index domain :  (i)
         comment      :  "Upper Bound of the Time Window" ;

      PARAMETER:
         identifier   :  XCoordinate
         index domain :  (i) ;

      PARAMETER:
         identifier   :  YCoordinate
         index domain :  (j) ;

      PARAMETER:
         identifier   :  c
         index domain :  (i,j)
         definition   :  floor(sqrt((XCoordinate(i) - XCoordinate(j))^2 + (YCoordinate(i) - YCoordinate(j))^2)*10)/10
         comment      :  "Cost or distance between i and j (round to first decimal)" ;

      PARAMETER:
         identifier   :  t
         index domain :  i
         comment      :  "Service Time" ;

      PARAMETER:
         identifier   :  Capacity
         definition   :  200 ;

      PARAMETER:
         identifier   :  M
         definition   :  1000
         comment      :  "A large constant" ;

      VARIABLE:
         identifier   :  x
         index domain :  (i,j) | i<>j
         range        :  binary
         comment      :  "Equal to 1 if arc (i,j) is used by a vehicle and 0 otherwise" ;

      VARIABLE:
         identifier   :  D
         index domain :  (i)
         range        :  nonnegative
         comment      :  "Departure time at customer i" ;

      VARIABLE:
         identifier   :  y
         index domain :  (i)
         range        :  nonnegative
         comment      :  "Load of the vehicle arriving at i" ;

      VARIABLE:
         identifier   :  TotalCost
         range        :  free
         definition   :  sum((i,j), c(i, j) * x(i, j)) ;

      CONSTRAINT:
         identifier   :  C1
         index domain :  i | i>0
         definition   :  sum(j, x(i, j)) = 1
         comment      :  "Correspond to a minimum cost flow problem" ;

      CONSTRAINT:
         identifier   :  C2
         index domain :  i | i>0
         definition   :  sum(j, x(i, j)) - sum(j, x(j, i)) = 0
         comment      :  "Correspond to a minimum cost flow problem" ;

      CONSTRAINT:
         identifier   :  C3
         index domain :  (i,j) | i>0 and j>0 and i<>j
         definition   :  D(i) + t(i) - D(j) <= M *(1 - x(i, j))
         comment      :  "Ensures feasibility of the schedule" ;

      CONSTRAINT:
         identifier   :  C4
         index domain :  i | i>0
         definition   :  D(i) >= LBTW(i)
         comment      :  "Ensures feasibility of the schedule" ;

      CONSTRAINT:
         identifier   :  C4p
         index domain :  i | i>0
         definition   :  D(i) <= UBTW(i)
         comment      :  "Ensures feasibility of the schedule" ;

      CONSTRAINT:
         identifier   :  C5
         index domain :  (i,j) | i>0 and j>0 and i<>j
         definition   :  y(i) + Demand(i) - y(j) <= M *(1 - x(i, j))
         comment      :  "Ensures feasibility of the loads" ;

      CONSTRAINT:
         identifier   :  C6
         index domain :  i | i>0
         definition   :  y(i) <= Capacity
         comment      :  "Ensures feasibility of the loads" ;

      MATHEMATICAL PROGRAM:
         identifier   :  LeastCost
         objective    :  TotalCost
         direction    :  minimize
         constraints  :  AllConstraints
         variables    :  AllVariables
         type         :  Automatic ;

    ENDSECTION  ;

  ENDSECTION VRPTW_Model ;

  SECTION User_Interface


    PROCEDURE
      identifier :  ColorsAssignment

      DECLARATION SECTION 

        PARAMETER:
           identifier   :  Counter
           range        :  integer ;

        PARAMETER:
           identifier   :  XCopy
           index domain :  (i,j) ;

        ELEMENT PARAMETER:
           identifier   :  CurrentCustomer
           range        :  Customers ;

        ELEMENT PARAMETER:
           identifier   :  NextCustomer
           range        :  Customers ;

        ELEMENT PARAMETER:
           identifier   :  NextCustomerTemp
           range        :  Customers ;

      ENDSECTION  ;

      body       :  
        empty RoutesColor;
        Counter := 0;
        for(i in Customers) do
                if(x(0,i) = 1) then
                        RoutesColor(0,i) := ColorsValue(Counter);
                        NextCustomer := First(j | x(i,j) );
                        CurrentCustomer := i;
                        while(NextCustomer <> 0) do
                                RoutesColor(CurrentCustomer,NextCustomer) := ColorsValue(Counter);
                                NextCustomerTemp := NextCustomer;
                                NextCustomer := First(j | x(CurrentCustomer,j) );
                                CurrentCustomer := NextCustomerTemp;
                        endwhile;
                        RoutesColor(CurrentCustomer,0) := ColorsValue(Counter);
                        Counter := Counter + 1;
                endif;
        endfor;

    ENDPROCEDURE  ;

    DECLARATION SECTION 

      SET:
         identifier   :  Colors
         subset of    :  Integers
         index        :  n
         definition   :  {0..NumberOfColors} ;

      PARAMETER:
         identifier   :  NumberOfColors
         definition   :  5 ;

      ELEMENT PARAMETER:
         identifier   :  ColorsValue
         index domain :  n
         range        :  AllColors
         definition   :  data { 0 : red,  1 : blue,  2 : green,  3 : yellow,  4 : magenta,  5 : cyan } ;

      ELEMENT PARAMETER:
         identifier   :  RoutesColor
         index domain :  (i,j)
         range        :  AllColors ;

    ENDSECTION  ;

  ENDSECTION User_Interface ;

  PROCEDURE
    identifier :  MainInitialization
    body       :  
      COMPOSITE TABLE
         i     XCoordinate      YCoordinate    Demand    LBTW    UBTW      t
      !  --    -----------      -----------     ---      ---      ---     ---
          0        40               50           0        0      240       0
          1        25               85          20      145      175      10
          2        22               75          30       50       80      10
          3        22               85          10      109      139      10
          4        20               80          40      141      171      10
          5        20               85          20       41       71      10
          6        18               75          20       95      125      10
          7        15               75          20       79      109      10
          8        15               80          10       91      121      10
          9        10               35          20       91      121      10
         10        10               40          30      119      149      10
         11         8               40          40       59       89      10
         12         8               45          20       64       94      10
         13         5               35          10      142      172      10
         14         5               45          10       35       65      10
         15         2               40          20       58       88      10
         16         0               40          20       72      102      10
         17         0               45          20      149      179      10
         18        44                5          20       87      117      10
         19        42               10          40       72      102      10
         20        42               15          10      122      152      10
         21        40                5          10       67       97      10
         22        40               15          40       92      122      10
         23        38                5          30       65       95      10
         24        38               15          10      148      178      10
         25        35                5          20      154      184      10
         ;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      /*x(i,j) := 0;
      
      x(0,2) := 1;
      x(2,5) := 1;
      x(5,7) := 1;
      x(7,6) := 1;
      x(6,8) := 1;
      x(8,3) := 1;
      x(3,1) := 1;
      x(1,4) := 1;
      x(4,0) := 1;
      
      x(0,14) := 1;
      x(14,12) := 1;
      x(12,15) := 1;
      x(15,16) := 1;
      x(16,9) := 1;
      x(9,10) := 1;
      x(10,13) := 1;
      x(13,17) := 1;
      x(17,0) := 1;
      
      x(0,11) := 1;
      x(11,22) := 1;
      x(22,20) := 1;
      x(20,0) := 1;
      
      x(0,23) := 1;
      x(23,21) := 1;
      x(21,19) := 1;
      x(19,18) := 1;
      x(18,25) := 1;
      x(25,24) := 1;
      x(24,0) := 1;
      
      x(i,j).nonvar := -1;
      */
      solve LeastCost;
      ColorsAssignment ;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_VRPTW ;
