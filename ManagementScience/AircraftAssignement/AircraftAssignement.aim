
MAIN MODEL Main_AircraftAssignement

  DECLARATION SECTION 

    SET:
       identifier   :  Aircraft
       index        :  a
       initial data :  data { a, b, c, d, e } ;

    SET:
       identifier   :  Route
       index        :  r
       initial data :  data { route-1, route-2, route-3, route-4, route-5 } ;

    PARAMETER:
       identifier   :  Availability
       index domain :  a
       initial data :  data { a : 10,  b : 19,  c : 25,  d : 15 } ;

    PARAMETER:
       identifier   :  Demand
       index domain :  r
       initial data :  data { route-1 : 250,  route-2 : 120,  route-3 : 180,  route-4 : 90,  route-5 : 600 } ;

    PARAMETER:
       identifier   :  Capabilities
       index domain :  (a,r)
       initial data :  data
                       { ( a, route-1 ) : 16,  ( a, route-2 ) : 15,  ( a, route-3 ) : 28,  ( a, route-4 ) : 23,  ( a, route-5 ) : 81,
                         ( b, route-2 ) : 10,  ( b, route-3 ) : 14,  ( b, route-4 ) : 15,  ( b, route-5 ) : 57,  ( c, route-2 ) :  5,
                         ( c, route-4 ) :  7,  ( c, route-5 ) : 29,  ( d, route-1 ) :  9,  ( d, route-2 ) : 11,  ( d, route-3 ) : 22,
                         ( d, route-4 ) : 17,  ( d, route-5 ) : 55,  ( e, route-1 ) :  1,  ( e, route-2 ) :  1,  ( e, route-3 ) :  1,
                         ( e, route-4 ) :  1,  ( e, route-5 ) :  1 } ;

    PARAMETER:
       identifier   :  Costs
       index domain :  (a,r)
       initial data :  data
                       { ( a, route-1 ) : 18,  ( a, route-2 ) : 21,  ( a, route-3 ) : 18,  ( a, route-4 ) : 16,  ( a, route-5 ) : 10,
                         ( b, route-2 ) : 15,  ( b, route-3 ) : 16,  ( b, route-4 ) : 14,  ( b, route-5 ) :  9,  ( c, route-2 ) : 10,
                         ( c, route-4 ) :  9,  ( c, route-5 ) :  6,  ( d, route-1 ) : 17,  ( d, route-2 ) : 16,  ( d, route-3 ) : 17,
                         ( d, route-4 ) : 15,  ( d, route-5 ) : 10,  ( e, route-1 ) : 13,  ( e, route-2 ) : 13,  ( e, route-3 ) :  7,
                         ( e, route-4 ) :  7,  ( e, route-5 ) :  1 } ;

    VARIABLE:
       identifier   :  Allocation
       index domain :  (a,r)
       range        :  nonnegative ;

    VARIABLE:
       identifier   :  TotalCost
       range        :  free
       definition   :  sum((a,r), Costs(a,r)*Allocation(a,r)) ;

    CONSTRAINT:
       identifier   :  AvailabilityConstraint
       index domain :  (a)|a <> Last(Aircraft)
       definition   :  sum(r, Allocation(a,r)) <= Availability(a)  ;

    CONSTRAINT:
       identifier   :  DemandConstraint
       index domain :  (r)
       definition   :  sum(a, Allocation(a,r)*Capabilities(a,r)) = Demand(r) ;

    MATHEMATICAL PROGRAM:
       identifier   :  LeastTotalCost
       objective    :  TotalCost
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  Automatic ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      solve LeastTotalCost;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_AircraftAssignement ;
