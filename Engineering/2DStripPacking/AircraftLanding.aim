
MAIN MODEL Main_AircraftLanding

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  NumberOfAircrafts
       definition   :  10 ;

    SET:
       identifier   :  Aircrafts
       subset of    :  Integers
       indices      :  i, j
       definition   :  {1..NumberOfAircrafts} ;

    PARAMETER:
       identifier   :  EarliestLanding
       index domain :  (i) ;

    PARAMETER:
       identifier   :  LatestLanding
       index domain :  (i) ;

    PARAMETER:
       identifier   :  TargetLanding
       index domain :  (i) ;

    PARAMETER:
       identifier   :  PenaltyAfterTarget
       index domain :  (i) ;

    PARAMETER:
       identifier   :  PenaltyBeforeTarget
       index domain :  (i) ;

    PARAMETER:
       identifier   :  SeparationTime
       index domain :  (i,j) ;

    VARIABLE:
       identifier   :  Landing
       index domain :  (i)
       range        :  nonnegative ;

    VARIABLE:
       identifier   :  AircraftsOrder
       index domain :  (i,j)
       range        :  binary ;

    VARIABLE:
       identifier   :  Early
       index domain :  (i)
       range        :  nonnegative ;

    VARIABLE:
       identifier   :  Late
       index domain :  (i)
       range        :  nonnegative ;

    VARIABLE:
       identifier   :  TotalCost
       range        :  free
       definition   :  sum(i, PenaltyBeforeTarget(i) * Early(i) + PenaltyAfterTarget(i) * Late(i)) ;

    CONSTRAINT:
       identifier   :  OrderConstraint
       index domain :  (i,j) | j > i
       definition   :  AircraftsOrder(i, j) + AircraftsOrder(j, i) = 1 ;

    CONSTRAINT:
       identifier   :  SeparationConstraint
       index domain :  (i,j) | i<>j
       definition   :  Landing(j) >= Landing(i)
                       + SeparationTime(i, j) * AircraftsOrder(i, j)
                       - (LatestLanding(i) - EarliestLanding(j)) * AircraftsOrder(j, i) ;

    CONSTRAINT:
       identifier   :  LowerTimeWindowConstraint
       index domain :  (i)
       definition   :  Landing(i) >= EarliestLanding(i) ;

    CONSTRAINT:
       identifier   :  UpperTimeWindowConstraint
       index domain :  (i)
       definition   :  Landing(i) <= LatestLanding(i) ;

    CONSTRAINT:
       identifier   :  EarlyContraint
       index domain :  i
       definition   :  Early(i) >= TargetLanding(i) - Landing(i) ;

    CONSTRAINT:
       identifier   :  LateContraint
       index domain :  i
       definition   :  Late(i) >= Landing(i) - TargetLanding(i) ;

    MATHEMATICAL PROGRAM:
       identifier   :  LeastCost
       objective    :  TotalCost
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  Automatic ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization
    body       :  
       COMPOSITE TABLE
      
       i   EarliestLanding   TargetLanding   LatestLanding   PenaltyBeforeTarget   PenaltyAfterTarget
      
       1         129              155            559                 10.00              10.00
       2         195              258            744                 10.00              10.00
       3         89               98             510                 30.00              30.00
       4         96               106            521                 30.00              30.00
       5         110              123            555                 30.00              30.00
       6         120              135            576                 30.00              30.00
       7         124              138            577                 30.00              30.00
       8         126              140            573                 30.00              30.00
       9         135              150            591                 30.00              30.00
       10        160              180            657                 30.00              30.00
       ;
      
      SeparationTime(i,j) := 8;
      SeparationTime(i,1) := 15;
      SeparationTime(i,2) := 15;
      SeparationTime(1,j) := 15;
      SeparationTime(2,j) := 15;
      SeparationTime(i,i) := 99999;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      solve LeastCost;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_AircraftLanding ;
