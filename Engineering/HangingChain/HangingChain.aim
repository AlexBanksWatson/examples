
MAIN MODEL Main_HangingChain

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  NumberOfIntervals
       range        :  integer
       initial data :  50 ;

    SET:
       identifier   :  Intervals
       subset of    :  Integers
       index        :  i
       definition   :  {0..NumberOfIntervals} ;

    PARAMETER:
       identifier   :  Bla
       index domain :  i
       definition   :  i/NumberOfIntervals ;

    PARAMETER:
       identifier   :  LengthOfChain
       initial data :  4 ;

    PARAMETER:
       identifier   :  Height0
       initial data :  1 ;

    PARAMETER:
       identifier   :  Height1
       initial data :  3 ;

    PARAMETER:
       identifier   :  IntervalLength
       definition   :  1/NumberOfIntervals ;

    PARAMETER:
       identifier   :  TMin
       definition   :  if(Height1 > Height0) then
                       0.25
                       else
                       0.75
                       endif ;

    VARIABLE:
       identifier   :  X
       index domain :  i
       range        :  free
       definition   :  if(i=0) then
                       Height0
                       else
                       X(i-1) + 0.5 * IntervalLength * (U(i-1) + U(i))
                       endif ;

    VARIABLE:
       identifier   :  U
       index domain :  (i)
       range        :  free ;

    VARIABLE:
       identifier   :  PotentialEnergy
       range        :  free
       definition   :  0.5 * IntervalLength * sum(i | i>0, X(i-1) * sqrt(1 + sqr(U(i-1))) + X(i) * sqrt(1 + sqr(U(i)))) ;

    CONSTRAINT:
       identifier   :  LengthConstraint
       definition   :  0.5 * IntervalLength * sum(i | i>0, sqrt(1 + sqr(U(i-1))) +sqrt(1 + sqr(U(i)))) = LengthOfChain ;

    CONSTRAINT:
       identifier   :  XUpperBound
       definition   :  X(NumberOfIntervals) = Height1 ;

    MATHEMATICAL PROGRAM:
       identifier   :  LeastPotentialEnergy
       objective    :  PotentialEnergy
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  Automatic ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      solve LeastPotentialEnergy;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_HangingChain ;
