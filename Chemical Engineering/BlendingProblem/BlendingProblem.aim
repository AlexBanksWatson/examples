
MAIN MODEL Main_BlendingProblem
  comment :  "Minimization of the price of an alloy produced from other alloys available on the market."

  DECLARATION SECTION 

    SET:
       identifier   :  AlloysOnMarket
       index        :  a
       initial data :  data{A,B,C,D,E,F,G,H,I} ;

    SET:
       identifier   :  RequiredElements
       index        :  e
       initial data :  data{lead, zinc, tin} ;

    PARAMETER:
       identifier   :  CompositionData
       index domain :  (e,a)
       initial data :  data
                       { ( lead, A ) : 10,  ( lead, B ) : 10,  ( lead, C ) : 40,  ( lead, D ) : 60,  ( lead, E ) : 30,  ( lead, F ) : 30,
                         ( lead, G ) : 30,  ( lead, H ) : 50,  ( lead, I ) : 20,  ( zinc, A ) : 10,  ( zinc, B ) : 30,  ( zinc, C ) : 50,
                         ( zinc, D ) : 30,  ( zinc, E ) : 30,  ( zinc, F ) : 40,  ( zinc, G ) : 20,  ( zinc, H ) : 40,  ( zinc, I ) : 30,
                         ( tin , A ) : 80,  ( tin , B ) : 60,  ( tin , C ) : 10,  ( tin , D ) : 10,  ( tin , E ) : 40,  ( tin , F ) : 30,
                         ( tin , G ) : 50,  ( tin , H ) : 10,  ( tin , I ) : 50 } ;

    PARAMETER:
       identifier   :  CompositionDataPercentage
       index domain :  (e,a)
       definition   :  CompositionData(e,a)/100  ;

    PARAMETER:
       identifier   :  DesiredBlendPercentage
       index domain :  (e) ;

    PARAMETER:
       identifier   :  AlloyPrice
       index domain :  (a)
       initial data :  data { A : 4.100,  B : 4.300,  C : 5.800,  D : 6.000,  E : 7.600,  F : 7.500,  G : 7.300,  H : 6.900,  I : 7.300 } ;

    VARIABLE:
       identifier   :  AlloyPurchased
       index domain :  (a)
       range        :  nonnegative ;

    VARIABLE:
       identifier   :  TotalCost
       range        :  free
       definition   :  sum(a, AlloyPrice(a)*AlloyPurchased(a)) ;

    CONSTRAINT:
       identifier   :  PurchaseConstraint
       index domain :  e
       definition   :  sum(a, CompositionDataPercentage(e,a)*AlloyPurchased(a)) = DesiredBlendPercentage(e) ;

    CONSTRAINT:
       identifier   :  MaterialBalance
       definition   :  sum(a, AlloyPurchased(a)) = 1 ;

    MATHEMATICAL PROGRAM:
       identifier   :  LeastCost
       objective    :  TotalCost
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  Automatic ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      solve LeastCost;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_BlendingProblem ;
