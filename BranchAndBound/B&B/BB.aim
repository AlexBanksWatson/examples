SECTION

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  NumberOfNodes
       range        :  integer ;

    ELEMENT PARAMETER:
       identifier   :  GMPBB
       range        :  AllGeneratedMathematicalPrograms ;

    ELEMENT PARAMETER:
       identifier   :  pcBB
       range        :  AllProgressCategories ;

    SET:
       identifier   :  ActiveNodes
       subset of    :  Integers
       index        :  an ;

    ELEMENT PARAMETER:
       identifier   :  CurrentNode
       range        :  ActiveNodes ;

    ELEMENT PARAMETER:
       identifier   :  LastActiveNode
       range        :  ActiveNodes ;

    SET:
       identifier   :  ChildNodes
       subset of    :  Integers
       index        :  cn ;

    PARAMETER:
       identifier   :  Child1
       range        :  integer ;

    PARAMETER:
       identifier   :  Child2
       range        :  integer ;

    SET:
       identifier   :  ColNrs
       subset of    :  Integers
       index        :  c ;

    PARAMETER:
       identifier   :  ActiveNodesEvaluation
       index domain :  an ;

    PARAMETER:
       identifier   :  BestObjective ;

    PARAMETER:
       identifier   :  LB
       index domain :  (c,an) ;

    PARAMETER:
       identifier   :  UB
       index domain :  (c,an) ;

    PARAMETER:
       identifier   :  LEV
       index domain :  (c,an) ;

    PARAMETER:
       identifier   :  NodeObj
       index domain :  an ;

    PARAMETER:
       identifier   :  IsInteger ;

    PARAMETER:
       identifier   :  BestBound ;

    PARAMETER:
       identifier   :  value ;

    PARAMETER:
       identifier   :  eps
       initial data :  1e-6 ;

    SET:
       identifier   :  OptimalityStatuses
       subset of    :  AllSolutionStates
       initial data :  { 'Optimal', 'LocallyOptimal', 'IntermediateNonOptimal' } ;

    ELEMENT PARAMETER:
       identifier   :  ProgramStatus
       range        :  AllSolutionStates ;

    PARAMETER:
       identifier   :  FoundSolution
       range        :  binary ;

    PARAMETER:
       identifier   :  RelativeGap
       definition   :  if ( abs( BestObjective - BestBound ) <= 1e-8 ) then
                               0
                       else
                               100 * abs( BestObjective - BestBound ) / ( 1 + abs(BestObjective) )
                       endif ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  BB
    arguments  :  (myGMP)

    DECLARATION SECTION 

      ELEMENT PARAMETER:
         identifier   :  myGMP
         range        :  AllGeneratedMathematicalPrograms
         property     :  Input ;

      ELEMENT PARAMETER:
         identifier   :  SolSesBB
         range        :  AllSolverSessions ;

      PARAMETER:
         identifier   :  Obj ;

      PARAMETER:
         identifier   :  HalfGap
         index domain :  c ;

      ELEMENT PARAMETER:
         identifier   :  MostFractionalColumn
         range        :  ColNrs ;

    ENDSECTION  ;

    body       :  
      GMPBB := GMP::Instance::CreatePresolved( myGMP, 'BBPresolved' );
      GMP::Instance::SetMathematicalProgrammingType( GMPBB, 'rminlp' );
      ColNrs := GMP::Instance::GetColumnNumbers( GMPBB, AllIntegerVariables );
      
      !Initialization progress window
      SolSesBB := GMP::Instance::CreateSolverSession( myGMP );
      pcBB := GMP::SolverSession::CreateProgressCategory( SolSesBB );
      GMP::ProgressWindow::DisplaySolver( "Branch-and-Bound", pcBB );
      
      !Initialization
      ActiveNodes := {1};
      BestObjective := 1e30;
      NumberOfNodes := 1;
      
      !Initial node
      LB(c,1) := 0;
      UB(c,1) := 1;
      for(c) do
              GMP::Column::SetLowerBound(GMPBB,c,LB(C,1));
              GMP::Column::SetUpperBound(GMPBB,c,UB(C,1));
      endfor;
      
      
      !Initial node test
      GMP::Instance::Solve(GMPBB);
      halt;
      IsInteger := GMP::Solution::IsInteger(GMPBB,1);
      ProgramStatus := GMP::Solution::GetProgramStatus(GMPBB, 1);
      
      FoundSolution := 0;
      Obj := GMP::Solution::GetObjective(GMPBB, 1);
      NodeObj(1) := Obj;
      
      if(ProgramStatus in OptimalityStatuses) then
              if (IsInteger) then
                      BestObjective := Obj;
                      GMP::Solution::RetrieveFromModel( myGMP, 1 );
                      FoundSolution := 1;
                      ActiveNodes := {};
              else
                      for(c) do
                              LEV(c,1) := GMP::Solution::GetColumnValue( GMPBB, 1, c );
                      endfor;
              endif;
      else
              GMP::Solution::SetProgramStatus( myGMP, 1, 'Infeasible' );
              ActiveNodes := {};
      endif;
      
      ! Main Loop
      while(ActiveNodes <> {})do
      
              CurrentNode := ArgMin( an, NodeObj(an) );
              BestBound := NodeObj(CurrentNode);
              Progress;
              LastActiveNode := Last(ActiveNodes);
      
              !First child of the CurrentNode
              Child1 := val(LastActiveNode) + 1;
              ChildNodes := Child1;
              !Second child of the CurrentNode
              Child2 := val(LastActiveNode) + 2;
              ChildNodes += Child2;
      
              ActiveNodes += ChildNodes;
      
              for (cn) do
                      LB(c,cn) := LB(c,CurrentNode);
                      UB(c,cn) := UB(c,CurrentNode);
              endfor;
      
              HalfGap(c) := abs(LEV(c,CurrentNode) - 0.5);
              MostFractionalColumn := ArgMin(c, HalfGap(c));
      
              UB(MostFractionalColumn,Child1) := 0;
              LB(MostFractionalColumn,Child2) := 1;
      
              for (cn) do
                      for(c) do
                              GMP::Column::SetLowerBound(GMPBB,c,LB(c,cn));
                              GMP::Column::SetUpperBound(GMPBB,c,UB(c,cn));
                      endfor;
      
                      GMP::Instance::Solve(GMPBB);
      
                      ProgramStatus := GMP::Solution::GetProgramStatus(GMPBB, 1);
                      Obj := GMP::Solution::GetObjective(GMPBB, 1);
      
                      if(ProgramStatus in OptimalityStatuses) then
                              if(Obj < BestObjective) then
                                      IsInteger := GMP::Solution::IsInteger(GMPBB,1);
      
                                      if (IsInteger) then
                                              ! Found new incumbent
                                              BestObjective := Obj;
                                              GMP::Solution::RetrieveFromModel( myGMP, 1 );
                                              FoundSolution := 1;
      
                                              ActiveNodes -= cn; ! Fathom node
                                              FathomNodesAfterNewIncumbent;
                                      else
                                              for(c) do
                                                      LEV(c,cn) := GMP::Solution::GetColumnValue( GMPBB, 1, c );
                                              endfor;
      
                                              NumberOfNodes += 1;
      
                                              NodeObj(cn) := Obj;
                                      endif;
                              else
                                      ActiveNodes -= cn;   ! Fathom node
                              endif;
                      else
                              ActiveNodes -= cn;   ! Fathom node
                      endif;
      
              endfor;
      
              ActiveNodes -= CurrentNode;
      
      endwhile;
      
      Progress;
      
      if ( FoundSolution ) then
              GMP::Solution::SendToModel( myGMP, 1 );
              GMP::Solution::SetProgramStatus( myGMP, 1, 'Optimal' );
      endif;
      
      GMP::Instance::Delete( GMPBB );

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  FathomNodesAfterNewIncumbent
    body       :  
      ActiveNodes -= { an | NodeObj(an) >= BestObjective };

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  Progress
    body       :  
      ValueString := FormatString("%i \t (# Active Nodes : %i)", NumberOfNodes, card(ActiveNodes));
      GMP::ProgressWindow::DisplayLine( 1, "# Nodes", ValueString, pcBB );
      GMP::ProgressWindow::DisplayLine( 2, "Best Bound", BestBound, pcBB );
      
      if ( FoundSolution ) then
              ValueString := FormatString( "%n \t (Gap: %.2n %%)", BestObjective, RelativeGap ) ;
      else
              ValueString := FormatString( "na" ) ;
      endif;
      
      GMP::ProgressWindow::DisplayLine( 3, "Best Objective", ValueString, pcBB );

  ENDPROCEDURE  ;

  DECLARATION SECTION 

    STRING PARAMETER:
       identifier :  ValueString ;

  ENDSECTION  ;

ENDSECTION  ;
