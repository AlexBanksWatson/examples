
MAIN MODEL Main_MultiLevelsBucket

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  NumberOfItems
       definition   :  40 ;

    PARAMETER:
       identifier   :  NumberOfPeriods
       definition   :  12 ;

    PARAMETER:
       identifier   :  NumberOfEndProducts
       definition   :  1 ;

    SET:
       identifier   :  Items
       subset of    :  Integers
       indices      :  i, j
       definition   :  {1..NumberOfItems} ;

    SET:
       identifier   :  Periods
       subset of    :  Integers
       index        :  t
       definition   :  {1..NumberOfPeriods} ;

    PARAMETER:
       identifier   :  IntermediateSUP
       index domain :  (i) ;

    PARAMETER:
       identifier   :  SetUpCost
       index domain :  i
       definition   :  0.1 * IntermediateSUP(i) ;

    PARAMETER:
       identifier   :  IntermediateIC
       index domain :  (i) ;

    PARAMETER:
       identifier   :  InventoryCost
       index domain :  (i)
       definition   :  0.1 * IntermediateIC(i) ;

    PARAMETER:
       identifier   :  BackloggingCost
       index domain :  (i)
       definition   :  10 * IntermediateIC(i) ;

    PARAMETER:
       identifier   :  Demand
       index domain :  (i,t) ;

    PARAMETER:
       identifier   :  TotalDemand
       index domain :  i
       definition   :  sum(t, Demand(i, t)) ;

    PARAMETER:
       identifier   :  Capacity
       definition   :  0.5 * TotalDemand(1) ;

    PARAMETER:
       identifier   :  OverallCapacity
       definition   :  5 * TotalDemand(1) ;

    PARAMETER:
       identifier   :  RML
       index domain :  (i,j) ;

    PARAMETER:
       identifier   :  IP
       index domain :  (i)
       definition   :  if (i <= NumberOfEndProducts) then
                       1
                       else
                       0
                       endif ;

    VARIABLE:
       identifier   :  X
       index domain :  (i,t)
       range        :  nonnegative ;

    VARIABLE:
       identifier   :  S
       index domain :  (i,t)
       range        :  nonnegative ;

    VARIABLE:
       identifier   :  R
       index domain :  (i,t) | IP(i)
       range        :  nonnegative ;

    VARIABLE:
       identifier   :  Y
       index domain :  (i,t)
       range        :  binary ;

    VARIABLE:
       identifier   :  TotalCost
       range        :  free
       definition   :  sum((i,t), SetUpCost(i) * Y(i, t) + InventoryCost(i) * S(i, t) + BackloggingCost(i) * R(i, t)) ;

    CONSTRAINT:
       identifier   :  FlowConstraint1
       index domain :  (i,t) | IP(i) =0
       definition   :  S(i, t-1) + X(i, t) = sum(j | j<>i, RML(i, j) * X(j, t)) + S(i, t) ;

    CONSTRAINT:
       identifier   :  FlowConstraint2
       index domain :  (i,t) | IP(i)
       definition   :  S(i, t-1) - R(i, t-1) + X(i, t) = Demand(i, t) + S(i, t) - R(i, t) ;

    CONSTRAINT:
       identifier   :  CapacityConstraint
       index domain :  (i,t)
       definition   :  X(i, t) <= Capacity * Y(i, t) ;

    CONSTRAINT:
       identifier   :  OverallCapacityConstraint
       index domain :  t
       definition   :  sum(i, X(i, t)) <= OverallCapacity ;

    MATHEMATICAL PROGRAM:
       identifier   :  LeastCost
       objective    :  TotalCost
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  Automatic ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization
    body       :  
          IntermediateSUP(i) := data
          {  1 : 282,   2 : 400,   3 : 477,   4 : 381,   5 : 536,   6 : 277,
             7 : 441,   8 : 340,   9 : 863,  10 : 167,  11 : 827,  12 : 479,
            13 : 212,  14 : 335,  15 : 902,  16 : 406,  17 : 733,  18 : 644,
            19 :  98,  20 : 538,  21 : 645,  22 : 871,  23 : 811,  24 : 665,
            25 : 505,  26 : 643,  27 : 346,  28 :  82,  29 : 824,  30 :  82,
            31 : 179,  32 : 115,  33 :  64,  34 : 815,  35 : 812,  36 : 393,
            37 : 716,  38 : 306,  39 : 487,  40 : 502 } ;
      
      
          IntermediateIC(i) := data
          {  1 : 0.200,   2 : 0.400,   3 : 0.300,   4 : 0.200,   5 : 0.400,
             6 : 0.200,   7 : 0.400,   8 : 0.200,   9 : 0.200,  10 : 0.300,
            11 : 0.200,  12 : 0.400,  13 : 0.300,  14 : 0.300,  15 : 0.300,
            16 : 0.200,  17 : 0.400,  18 : 0.400,  19 : 0.400,  20 : 0.200,
            21 : 0.300,  22 : 0.200,  23 : 0.300,  24 : 0.400,  25 : 0.300,
            26 : 0.300,  27 : 0.300,  28 : 0.400,  29 : 0.400,  30 : 0.200,
            31 : 0.200,  32 : 0.200,  33 : 0.400,  34 : 0.200,  35 : 0.400,
            36 : 0.200,  37 : 0.200,  38 : 0.300,  39 : 0.400,  40 : 0.400 } ;
      
      
          RML(i,j) := data
          { (  2,  1 ) : 1,  (  3,  1 ) : 1,  (  4,  1 ) : 1,  (  5,  2 ) : 1,
            (  6,  3 ) : 1,  (  7,  3 ) : 1,  (  8,  4 ) : 1,  (  9,  4 ) : 1,
            ( 10,  1 ) : 1,  ( 11,  2 ) : 1,  ( 12,  5 ) : 1,  ( 13,  5 ) : 1,
            ( 14,  6 ) : 1,  ( 15,  6 ) : 1,  ( 16,  4 ) : 1,  ( 17,  8 ) : 1,
            ( 18,  8 ) : 1,  ( 19, 10 ) : 1,  ( 20, 10 ) : 1,  ( 21,  7 ) : 1,
            ( 22,  7 ) : 1,  ( 23, 16 ) : 1,  ( 24, 16 ) : 1,  ( 25,  9 ) : 1,
            ( 26, 10 ) : 1,  ( 27, 19 ) : 1,  ( 28, 19 ) : 1,  ( 29, 20 ) : 1,
            ( 30, 20 ) : 1,  ( 31, 12 ) : 1,  ( 32, 12 ) : 1,  ( 33, 12 ) : 1,
            ( 34, 13 ) : 1,  ( 35, 13 ) : 1,  ( 36, 15 ) : 1,  ( 37, 15 ) : 1,
            ( 38, 17 ) : 1,  ( 39, 17 ) : 1,  ( 40,  9 ) : 1 } ;
      
      
      Demand(i,1)  :=  0  ;
      Demand(i,2)  :=  0  ;
      Demand(i,3)  :=  0  ;
      Demand(i,4)  :=  155;
      Demand(i,5)  :=  88 ;
      Demand(i,6)  :=  0  ;
      Demand(i,7)  :=  0  ;
      Demand(i,8)  :=  0  ;
      Demand(i,9)  :=  140;
      Demand(i,10) :=  0  ;
      Demand(i,11) :=  33 ;
      Demand(i,12) :=  0  ;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      solve LeastCost

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return DataManagementExit();

  ENDPROCEDURE  ;

ENDMODEL Main_MultiLevelsBucket ;
