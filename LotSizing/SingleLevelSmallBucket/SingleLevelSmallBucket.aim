
MAIN MODEL Main_SingleLevelSmallBucket
  comment :  "Lot sizing
              
              Problem type:
              MIP (hard)
              
              Description:
              Lot-sizing problems are production planning problems in which the periods
              are a priori, and production of an item in a given period implies some
              discrete event such as payment of a cost or the loss of a amount of
              production capacity, due to placement of an order, or the set-up, startup,
              or changeover of a machine.
              
              References:
              Problem Constantino from: Belvaux, G., L.A. Wolsey, Lotsizelib: A library of
              models and matrices for lot-sizing problems, Core discussion paper, Universite
              Catholique de Louvain, 1999."

  DECLARATION SECTION 

    PARAMETER:
       identifier   :  NumberOfItems
       definition   :  5 ;

    PARAMETER:
       identifier   :  NumberOfMachines
       definition   :  2 ;

    PARAMETER:
       identifier   :  NumberOfPeriods
       definition   :  12 ;

    SET:
       identifier   :  Items
       subset of    :  Integers
       index        :  i
       definition   :  {1..NumberOfItems} ;

    SET:
       identifier   :  Machines
       subset of    :  Integers
       index        :  m
       definition   :  {1..NumberOfMachines} ;

    SET:
       identifier   :  Periods
       subset of    :  Integers
       index        :  t
       definition   :  {1..NumberOfPeriods} ;

    PARAMETER:
       identifier   :  Demand
       index domain :  (i,t) ;

    PARAMETER:
       identifier   :  SetUpCost ;

    PARAMETER:
       identifier   :  StartUpCost ;

    PARAMETER:
       identifier   :  StockCost
       index domain :  (i)
       definition   :  data { 1 : 2,  2 : 1,  3 : 1,  4 : 1,  5 : 1 } ;

    PARAMETER:
       identifier   :  BackloggingCost
       index domain :  (i)
       definition   :  data { 1 : 800,  2 : 400,  3 : 400,  4 : 400,  5 : 400 } ;

    PARAMETER:
       identifier   :  Capacity
       index domain :  (m)
       definition   :  data { 1 : 7,  2 : 10 } ;

    PARAMETER:
       identifier   :  StartUpTime
       index domain :  (m)
       definition   :  data { 1 : 2,  2 : 4 } ;

    VARIABLE:
       identifier   :  S
       index domain :  (i,t)
       range        :  nonnegative
       comment      :  "Items stocked" ;

    VARIABLE:
       identifier   :  R
       index domain :  (i,t)
       range        :  nonnegative
       comment      :  "End products backlogged" ;

    VARIABLE:
       identifier   :  X
       index domain :  (i,t,m)
       range        :  nonnegative
       comment      :  "Number of items i produced on machine m in period t" ;

    VARIABLE:
       identifier   :  Y
       index domain :  (i,t,m)
       range        :  binary
       comment      :  "Production variable: 1 if item i is produced on machine m in period t" ;

    VARIABLE:
       identifier   :  Z
       index domain :  (i,t,m)
       range        :  binary
       comment      :  "Startup variable: 1 if production of item i is started in period t on machine m" ;

    VARIABLE:
       identifier   :  TotalCost
       range        :  free
       definition   :  sum((i,t,m), SetUpCost * Y(i, t, m) + StartUpCost * Z(i, t, m))
                       + sum((i,t), StockCost(i) * S(i, t) + BackloggingCost(i) * R(i, t)) ;

    CONSTRAINT:
       identifier   :  FlowConstraint
       index domain :  (i,t)
       definition   :  S(i, t-1) - R(i, t-1) + sum(m, X(i, t, m)) = Demand(i, t) + S(i, t) - R(i, t) ;

    CONSTRAINT:
       identifier   :  CapacityConstraint
       index domain :  (i,t,m)
       definition   :  X(i, t, m) + StartUpTime(m) * Z(i, t, m) <= Capacity(m) * Y(i, t, m) ;

    CONSTRAINT:
       identifier   :  MachineConstraint
       index domain :  (m,t)
       definition   :  sum(i, Y(i, t, m)) <= 1 ;

    CONSTRAINT:
       identifier   :  StartupConstraint1
       index domain :  (i,t,m)
       definition   :  Z(i, t, m) >= Y(i, t, m) - Y(i, t-1, m) ;

    CONSTRAINT:
       identifier   :  StartupConstraint2
       index domain :  (i,t,m)
       definition   :  Z(i, t, m) <= Y(i, t, m) ;

    CONSTRAINT:
       identifier   :  StartupConstraint3
       index domain :  (i,t,m)
       definition   :  Y(i, t-1, m) + Z(i, t, m) <= 1 ;

    MATHEMATICAL PROGRAM:
       identifier   :  LeastCost
       objective    :  TotalCost
       direction    :  minimize
       constraints  :  AllConstraints
       variables    :  AllVariables
       type         :  Automatic ;

  ENDSECTION  ;

  PROCEDURE
    identifier :  MainInitialization
    body       :  
      Demand(i,t) := DATA TABLE
      
          1     2     3     4     5     6     7     8     9     10    11    12
      !
      1  2.15  2.07  4.22  4.49  1.88  5.20  1.35  2.06  1.80  1.70  2.51  0.36
      2  1.92  1.88  1.97  1.74  1.41  2.44  1.69  0.75  1.15  2.02  0.51  0.65
      3  2.11  2.19  2.11  2.19  2.40  2.47  1.62  2.25  2.25  2.89  1.48  1.48
      4  0.75  0.92  0.72  0.75  1.39  0.46  0.89  2.35  2.69  0.42  0.46  3.70
      5  0.92  0.92  1.00  1.17  1.17  1.17  1.25  0.92  2.09  1.92  0.50  0.50
      ;
      
      SetUpCost := 100;
      
      StartUpCost := 200;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainExecution
    body       :  
      solve LeastCost;

  ENDPROCEDURE  ;

  PROCEDURE
    identifier :  MainTermination
    body       :  
      return 1;

  ENDPROCEDURE  ;

ENDMODEL Main_SingleLevelSmallBucket ;
